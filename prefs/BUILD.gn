

config("prefs_include_dirs") {
  include_dirs=["$root_gen_dir/vivaldi"]
}

action("prefs_files_generator") {
  prefs_definitions_json = "//vivaldi/vivapp/src/prefs_definitions.json"

  vivaldi_gen_prefs_cc = "$target_gen_dir/vivaldi_gen_prefs.cc"
  vivaldi_gen_prefs_h = "$target_gen_dir/vivaldi_gen_prefs.h"
  vivaldi_gen_pref_enums_h = "$target_gen_dir/vivaldi_gen_pref_enums.h"

  sources = [
    prefs_definitions_json,
  ]

  outputs = [
    vivaldi_gen_prefs_cc,
    vivaldi_gen_prefs_h,
    vivaldi_gen_pref_enums_h,
  ]

  script = "//vivaldi/tools/prefs_files_generator/prefs_files_generator.py"

  args = [
    "--prefs-definitions=" + rebase_path(prefs_definitions_json, root_build_dir),
    "--prefs-names-h=" + rebase_path(vivaldi_gen_prefs_h, root_build_dir),
    "--prefs-names-cc=" + rebase_path(vivaldi_gen_prefs_cc, root_build_dir),
    "--prefs-enums-h=" + rebase_path(vivaldi_gen_pref_enums_h, root_build_dir),
    "--root="+rebase_path(root_gen_dir, root_build_dir),
    "--target-os="+target_os,
  ]

  all_dependent_configs = [":prefs_include_dirs"]
}

static_library("prefs_files_generated") {
  sources = get_target_outputs(":prefs_files_generator")
  public_deps = [
    ":prefs_files_generator",
  ]
  deps = [
    "//base",
  ]
}

static_library("prefs") {
  sources = [
    "vivaldi_prefs_definitions.h",
    "vivaldi_prefs_definitions.cc",
    "vivaldi_local_state_prefs.h",
    "vivaldi_local_state_prefs.cc",
  ]
  if (is_mac) {
    sources += [
      "vivaldi_prefs_definitions_mac.mm",
    ]
  } else if (is_win) {
    sources += [
      "vivaldi_prefs_definitions_win.cc",
    ]
  } else {
    sources += [
      "vivaldi_prefs_definitions_common.cc",
    ]
  }
  public_deps = [
    ":prefs_files_generated",
  ]
  deps = [
    "//base",
    "//components/pref_registry",
    "//components/prefs",
    "//components/version_info",
    "//components/search_engines",
    "//skia",
    ":prefs_strings",
  ]
  if(!is_android && !is_ios) {
    deps += [ "//apps:apps" ]
  }
  if(!is_ios) {
    deps += [
    "//chrome/common:constants",
    ]
  }
}

static_library("prefs_strings") {
  sources = [
    "vivaldi_pref_names.cc",
    "vivaldi_pref_names.h",
  ]
}

static_library("browser_prefs") {
  sources = [
    "native_settings_observer.cc",
    "native_settings_observer.h",
    "vivaldi_tab_zoom_pref.cc",
    "vivaldi_tab_zoom_pref.h",
  ]
  if (is_linux) {
    sources += [
      "native_settings_observer_linux.cc",
      "native_settings_observer_linux.h",
    ]
  }
  if (is_mac) {
    sources += [
      "native_settings_observer_mac.h",
      "native_settings_observer_mac.mm",
      "native_settings_helper_mac.h",
      "native_settings_helper_mac.mm",
    ]
  }
  if (is_win) {
    sources += [
      "native_settings_observer_win.cc",
      "native_settings_observer_win.h",
      "native_settings_helper_win.h",
      "native_settings_helper_win.cc",
    ]
  }
  public_deps = [
    ":prefs_strings",
    ":prefs_files_generator",
  ]
  deps = [
    "//components/prefs",
    "//skia",
  ]
  if (!is_android && !is_ios) {
    deps += [
      "//vivaldi/extensions/schema",
    ]
  }
  if(!is_ios) {
    deps += [
      "//chrome/common:buildflags",
      "//content/public/browser",
    ]
  }
}
