import("//build/config/chrome_build.gni")
import("//vivaldi/gn/config/version.gni")

if (is_win) {
  if (is_official_build) {
    update_template_instance("//components/viz/test:test_support") {
      cflags = [ "/wd4723" ]  # potential divide by 0.
    }
  }

  update_target("//third_party/angle:copy_compiler_dll") {
    sources -= ["$windows_sdk_path/Redist/D3D/$target_cpu/d3dcompiler_47.dll"]
    sources += ["$windows_sdk_path/bin/10.0.17134.0/$target_cpu/d3dcompiler_47.dll"]
  }

  foreach(_target,
          [
            "//chrome/test:browser_tests",
            "//components:components_unittests",
            "//content/test:content_browsertests",
            "//content/test:content_unittests",
          ]) {
    update_template_instance(_target) {
      configs -= [ "//build/config/win:default_incremental_linking" ]
      configs += [ "//build/config/win:no_incremental_linking" ]
    }
  }
}

if (is_mac) {
  update_template_instance("//chrome:chrome_framework") {
    ldflags += [
      "-Wl,-rpath,@loader_path/../../../../Frameworks",
    ]
  }

  update_template_instance("//chrome/installer/mac:sign_config") {
    extra_args += [
      "-e", "GLOBAL_BUILDNUM=$GLOBAL_BUILDNUM",
    ]
  }

  foreach(_target,
          [
            "//chrome/test:browser_tests",
            "//chrome/test:interactive_ui_tests",
            "//chrome/test:sync_integration_tests",
            "//chrome/test:unit_tests",
            "//components:components_unittests",
          ]) {
    update_template_instance(_target) {
      if (!defined(ldflags)) {
        ldflags = []
      }
      ldflags += [
        "-Wl,-rpath,@executable_path/../../third_party/sparkle_lib/",
      ]
    }
  }
}

if (is_linux) {
  update_target("//chrome:chrome_initial") {
    # Find libffmpeg.so without the executable wrapper script. Note that
    # RUNPATH (new-dtags) is used over RPATH because otherwise there will be a
    # failure when building packages.
    ldflags += [
      "-rpath",
      "\$ORIGIN/lib/", # Linux packages use lib/libffmpeg.so
      "-rpath",
      "\$ORIGIN/", # Local builds use libffmpeg.so
      "-Wl,--enable-new-dtags",
    ]
  }
  update_target("//tools/v8_context_snapshot:v8_context_snapshot_generator") {
    ldflags = [
      "-rpath",
      "\$ORIGIN/", # Find libffmpeg.so
    ]
  }
  if (target_cpu == "arm64") {
    update_template_instance("//third_party/swiftshader/third_party/llvm-7.0:swiftshader_llvm") {
      if (!defined(cflags)) {
        cflags = []
      }
      cflags += [ "-Wno-defaulted-function-deleted" ]
    }
    update_template_instance("//third_party/swiftshader/src/Reactor:swiftshader_reactor") {
      if (!defined(cflags)) {
        cflags = []
      }
      cflags += [ "-Wno-defaulted-function-deleted" ]
    }
  }
}
