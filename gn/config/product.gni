declare_args() {
  build_branch = "master"
  force_sopranos_build = false
}

_vivaldi_version_py_path = "//build/util/version.py"

_vivaldi_product_kind_file = "//vivaldi/VIVALDI_PRODUCT"

_vivaldi_release_py_path = "//vivaldi/scripts/get_preview.py"

release_kind = "normal"
# Only check branch if this is an official build and not a forced sopranos android build
if (is_official_build && !force_sopranos_build) {
  release_kind =
    exec_script(_vivaldi_release_py_path, [ build_branch ], "trim string")
}
official_product_kind_string =
    exec_script(_vivaldi_version_py_path,
                [
                  "-f",
                  rebase_path(_vivaldi_product_kind_file, root_build_dir),
                  "-t",
                  "@VIVALDI_PRODUCT@",
                ],
                "trim string",
                [_vivaldi_product_kind_file])

if (is_vivaldi_polestar) {
  # Vivaldi/Polestar
  android_vivaldi_manifest_package = "com.polestar.vivaldi.dev.android"
} else {
  android_vivaldi_manifest_package = "com.vivaldi.browser.sopranos"
}

is_final_build = false
_is_snapshot = false
_is_dev = false
if (is_official_build && release_kind == "final") {
  is_final_build = true
  VIVALDI_RELEASE_KIND = "vivaldi_final"
  if (is_vivaldi_polestar) {
    # Vivaldi/Polestar
    android_vivaldi_manifest_package = "com.polestar.vivaldi.production.android"
  } else {
    android_vivaldi_manifest_package = "com.vivaldi.browser"
  }
} else if (is_official_build && !force_sopranos_build) {
  _is_snapshot = true
  VIVALDI_RELEASE_KIND = "vivaldi_snapshot"
  if (is_vivaldi_polestar) {
    # Vivaldi/Polestar
    android_vivaldi_manifest_package = "com.polestar.vivaldi.beta.android"
  } else {
    android_vivaldi_manifest_package = "com.vivaldi.browser.snapshot"
  }
} else {
  _is_dev = true
  VIVALDI_RELEASE_KIND = "vivaldi_sopranos"
}
extra_grit_defines = [
  "-D",
  "vivaldi_release_final=${is_final_build}",
  "-D",
  "vivaldi_release_snapshot=${_is_snapshot}",
  "-D",
  "vivaldi_release_sopranos=${_is_dev}",
  "-D",
  "vivaldi_debug=${is_debug}"
]

if (is_vivaldi_automotive && !is_vivaldi_polestar) {
  android_vivaldi_manifest_package += ".automotive"
  if (is_vivaldi_renault) {
    android_vivaldi_manifest_package += ".renault"
  }
  if (is_vivaldi_lynkco) {
    android_vivaldi_manifest_package += ".lynkco"
  }
  if (is_vivaldi_mercedes) {
    android_vivaldi_manifest_package += ".mercedes"
  }
  if (is_vivaldi_volvo) {
    android_vivaldi_manifest_package += ".volvo"
  }
  if (is_vivaldi_mahindra) {
    android_vivaldi_manifest_package += ".mahindra"
  }
}

if (is_ios) {
  ios_primary_app_icon_name = ""
  # A flag to test local builds as a final build
  # if true, compiling will produce a stable build, not a snapshot
  # This should always be false when committing to main!
  test_ios_app_as_final = false
  if (release_kind == "final" || test_ios_app_as_final) {
    ios_primary_app_icon_name = "AppIcon-Final"
  } else {
    ios_primary_app_icon_name = "AppIcon-Snapshot"
  }
}
