# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")

html_to_wrapper("html_wrapper_files") {
  in_files = [ "aggregation_service_internals_table.html" ]
  template = "native"
}

# Copy (via creating sym links) all the other files into the same folder for
# ts_library.
copy("copy_files") {
  deps = [ "//content/browser/aggregation_service:mojo_bindings_js__generator" ]
  sources = [
    "$root_gen_dir/mojom-webui/content/browser/aggregation_service/aggregation_service_internals.mojom-webui.js",
    "aggregation_service_internals.ts",
    "aggregation_service_internals_table.ts",
    "table_model.ts",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "aggregation_service_internals.ts",
    "aggregation_service_internals_table.ts",
    "aggregation_service_internals_table.html.ts",
    "table_model.ts",
  ]

  # Generated .js files.
  in_files += [ "aggregation_service_internals.mojom-webui.js" ]
  deps = [
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  definitions = [ "//tools/typescript/definitions/chrome_send.d.ts" ]
  extra_deps = [
    ":copy_files",
    ":html_wrapper_files",
  ]
}
