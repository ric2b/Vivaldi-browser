# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//build/util/process_version.gni")

_version_constants_java_file =
    "$target_gen_dir/org/chromium/weblayer/WebLayerClientVersionConstants.java"

weblayer_client_manifest =
    "$target_gen_dir/weblayer_client_manifest/AndroidManifest.xml"

jinja_template("weblayer_client_manifest") {
  input = "AndroidManifest.xml"
  output = weblayer_client_manifest
}

android_resources("client_resources") {
  sources = [
    "res/values-night/colors.xml",
    "res/values-night/values.xml",
    "res/values-v27/styles.xml",
    "res/values-v28/styles.xml",
    "res/values/colors.xml",
    "res/values/ids.xml",
    "res/values/styles.xml",
    "res/values/values.xml",
    "res/xml/weblayer_file_paths.xml",
  ]
  android_manifest = weblayer_client_manifest
  android_manifest_dep = ":weblayer_client_manifest"
}

android_library("java") {
  sources = [
    "org/chromium/weblayer/ActionModeCallback.java",
    "org/chromium/weblayer/ActionModeItemType.java",
    "org/chromium/weblayer/BroadcastReceiver.java",
    "org/chromium/weblayer/Browser.java",
    "org/chromium/weblayer/BrowserControlsOffsetCallback.java",
    "org/chromium/weblayer/BrowserEmbeddabilityMode.java",
    "org/chromium/weblayer/BrowserFragment.java",
    "org/chromium/weblayer/BrowserFragmentCreateParams.java",
    "org/chromium/weblayer/BrowserRestoreCallback.java",
    "org/chromium/weblayer/BrowsingDataType.java",
    "org/chromium/weblayer/Callback.java",
    "org/chromium/weblayer/CaptureScreenShotCallback.java",
    "org/chromium/weblayer/ChildProcessService.java",
    "org/chromium/weblayer/ContextMenuParams.java",
    "org/chromium/weblayer/CookieChangeCause.java",
    "org/chromium/weblayer/CookieChangedCallback.java",
    "org/chromium/weblayer/CookieManager.java",
    "org/chromium/weblayer/CrashReporterCallback.java",
    "org/chromium/weblayer/CrashReporterController.java",
    "org/chromium/weblayer/DarkModeStrategy.java",
    "org/chromium/weblayer/Download.java",
    "org/chromium/weblayer/DownloadCallback.java",
    "org/chromium/weblayer/DownloadError.java",
    "org/chromium/weblayer/DownloadState.java",
    "org/chromium/weblayer/ErrorPage.java",
    "org/chromium/weblayer/ErrorPageCallback.java",
    "org/chromium/weblayer/ExternalIntentInIncognitoCallback.java",
    "org/chromium/weblayer/ExternalIntentInIncognitoUserDecision.java",
    "org/chromium/weblayer/FaviconCallback.java",
    "org/chromium/weblayer/FaviconFetcher.java",
    "org/chromium/weblayer/FindInPageCallback.java",
    "org/chromium/weblayer/FindInPageController.java",
    "org/chromium/weblayer/FullscreenCallback.java",
    "org/chromium/weblayer/GoogleAccountAccessTokenFetcher.java",
    "org/chromium/weblayer/GoogleAccountServiceType.java",
    "org/chromium/weblayer/GoogleAccountsCallback.java",
    "org/chromium/weblayer/GoogleAccountsParams.java",
    "org/chromium/weblayer/GooglePayDataCallbacksServiceWrapper.java",
    "org/chromium/weblayer/ImageDecoderService.java",
    "org/chromium/weblayer/LoadError.java",
    "org/chromium/weblayer/MediaCaptureCallback.java",
    "org/chromium/weblayer/MediaCaptureController.java",
    "org/chromium/weblayer/MediaPlaybackBaseService.java",
    "org/chromium/weblayer/MediaRouteDialogFragment.java",
    "org/chromium/weblayer/MediaSessionService.java",
    "org/chromium/weblayer/NavigateParams.java",
    "org/chromium/weblayer/Navigation.java",
    "org/chromium/weblayer/NavigationCallback.java",
    "org/chromium/weblayer/NavigationController.java",
    "org/chromium/weblayer/NavigationState.java",
    "org/chromium/weblayer/NewTabCallback.java",
    "org/chromium/weblayer/NewTabType.java",
    "org/chromium/weblayer/ObserverList.java",
    "org/chromium/weblayer/OpenUrlCallback.java",
    "org/chromium/weblayer/Page.java",
    "org/chromium/weblayer/PageInfoDisplayOptions.java",
    "org/chromium/weblayer/PaymentDetailsUpdateServiceWrapper.java",
    "org/chromium/weblayer/PrerenderController.java",
    "org/chromium/weblayer/Profile.java",
    "org/chromium/weblayer/RemoteFragment.java",
    "org/chromium/weblayer/RemoteMediaService.java",
    "org/chromium/weblayer/ScrollNotificationType.java",
    "org/chromium/weblayer/ScrollOffsetCallback.java",
    "org/chromium/weblayer/SettingType.java",
    "org/chromium/weblayer/SettingsActivity.java",
    "org/chromium/weblayer/SettingsFragment.java",
    "org/chromium/weblayer/SiteSettingsActivity.java",
    "org/chromium/weblayer/Tab.java",
    "org/chromium/weblayer/TabCallback.java",
    "org/chromium/weblayer/TabListCallback.java",
    "org/chromium/weblayer/ThreadCheck.java",
    "org/chromium/weblayer/UnsupportedVersionException.java",
    "org/chromium/weblayer/UrlBarController.java",
    "org/chromium/weblayer/UrlBarOptions.java",
    "org/chromium/weblayer/UserIdentityCallback.java",
    "org/chromium/weblayer/VerifiesOnO.java",
    "org/chromium/weblayer/VerifiesOnR.java",
    "org/chromium/weblayer/WebLayer.java",
    "org/chromium/weblayer/WebLayerFileProvider.java",
    "org/chromium/weblayer/WebMessage.java",
    "org/chromium/weblayer/WebMessageCallback.java",
    "org/chromium/weblayer/WebMessageReplyProxy.java",
    "org/chromium/weblayer/WebViewCompatibilityHelper.java",
    _version_constants_java_file,
  ]

  resources_package = "org.chromium.weblayer"
  deps = [
    ":client_resources",
    ":client_version",
    ":weblayer_client_manifest",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_appcompat_appcompat_java",
    "//third_party/androidx:androidx_core_core_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//third_party/androidx:androidx_lifecycle_lifecycle_common_java",
    "//third_party/androidx:androidx_lifecycle_lifecycle_viewmodel_java",
    "//weblayer/browser/java:base_module_interfaces_java",
    "//weblayer/browser/java:interfaces_java",
  ]

  proguard_configs = [ "proguard.flags" ]
}

dist_aar("client_aar") {
  deps = [
    ":java",
    ":weblayer_client_manifest",
  ]

  # The aar should only include weblayer files. The client will provide the
  # support library.
  jar_included_patterns = [
    "org/chromium/weblayer/*",
    "org/chromium/weblayer_private/*",
  ]
  resource_included_patterns = [ "*/weblayer/public/java/*" ]

  # R files are excluded because they are not used in the client lib and depend
  # on other generated chromium R classes.
  jar_excluded_patterns = [
    "org/chromium/weblayer/client/R\$*.class",
    "org/chromium/weblayer/client/R.class",
  ]

  # dist_aar does not take the manifest from libraries in deps, so we have to
  # specify again here.
  android_manifest = weblayer_client_manifest
  output = "$root_build_dir/WebLayerClient.aar"
}

process_version("client_version") {
  process_only = true
  template_file =
      "org/chromium/weblayer/WebLayerClientVersionConstants.java.version"
  output = _version_constants_java_file
  sources = [ "//chrome/VERSION" ]
}

android_aidl("browserfragment_aidl") {
  import_include = [ "." ]
  sources = [
    "org/chromium/browserfragment/interfaces/IBooleanCallback.aidl",
    "org/chromium/browserfragment/interfaces/IBrowserFragmentDelegate.aidl",
    "org/chromium/browserfragment/interfaces/IBrowserFragmentDelegateClient.aidl",
    "org/chromium/browserfragment/interfaces/IBrowserSandboxCallback.aidl",
    "org/chromium/browserfragment/interfaces/IBrowserSandboxService.aidl",
    "org/chromium/browserfragment/interfaces/IFragmentParams.aidl",
    "org/chromium/browserfragment/interfaces/IStringCallback.aidl",
    "org/chromium/browserfragment/interfaces/ITabCallback.aidl",
    "org/chromium/browserfragment/interfaces/ITabNavigationControllerProxy.aidl",
    "org/chromium/browserfragment/interfaces/ITabObserverDelegate.aidl",
    "org/chromium/browserfragment/interfaces/ITabParams.aidl",
    "org/chromium/browserfragment/interfaces/ITabProxy.aidl",
    "org/chromium/browserfragment/interfaces/IWebMessageCallback.aidl",
    "org/chromium/browserfragment/interfaces/IWebMessageReplyProxy.aidl",
  ]
}

android_library("browserfragment_interfaces_java") {
  deps = [ "//base:base_java" ]
  srcjar_deps = [ ":browserfragment_aidl" ]
}

android_library("browserfragment_java") {
  sources = [
    "org/chromium/browserfragment/Browser.java",
    "org/chromium/browserfragment/BrowserFragment.java",
    "org/chromium/browserfragment/FragmentParams.java",
    "org/chromium/browserfragment/Tab.java",
    "org/chromium/browserfragment/TabManager.java",
    "org/chromium/browserfragment/TabNavigationController.java",
    "org/chromium/browserfragment/TabObserver.java",
    "org/chromium/browserfragment/TabObserverDelegate.java",
    "org/chromium/browserfragment/WebMessageCallback.java",
    "org/chromium/browserfragment/WebMessageReplyProxy.java",
  ]

  resources_package = "org.chromium.browserfragment"
  deps = [
    ":browserfragment_interfaces_java",
    "//base:base_java",
    "//third_party/android_deps:com_google_guava_guava_android_java",
    "//third_party/android_deps:com_google_guava_listenablefuture_java",
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_appcompat_appcompat_java",
    "//third_party/androidx:androidx_concurrent_concurrent_futures_java",
    "//third_party/androidx:androidx_fragment_fragment_java",
    "//third_party/androidx:androidx_lifecycle_lifecycle_viewmodel_java",
  ]

  proguard_configs = [ "proguard.flags" ]
}

android_library("browserfragment_sandbox_java") {
  sources = [
    # Additional weblayer public files that are for the sandbox.
    "org/chromium/weblayer/BrowserFragmentDelegate.java",
    "org/chromium/weblayer/BrowserFragmentTabDelegate.java",
    "org/chromium/weblayer/BrowserSandboxService.java",
    "org/chromium/weblayer/TabNavigationControllerProxy.java",
    "org/chromium/weblayer/TabParams.java",
    "org/chromium/weblayer/TabProxy.java",
    "org/chromium/weblayer/WebMessageReplyProxyProxy.java",
  ]
  resources_package = "org.chromium.weblayer"
  deps = [
    ":browserfragment_interfaces_java",
    ":java",
    "//third_party/androidx:androidx_annotation_annotation_java",
  ]
}
