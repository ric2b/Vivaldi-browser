# Copyright 2014 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}

static_library("common") {
  sources = [
    "credential_manager_types.cc",
    "credential_manager_types.h",
    "password_manager_pref_names.cc",
    "password_manager_pref_names.h",
    "password_manager_ui.h",
    "password_manager_util.cc",
    "password_manager_util.h",
  ]

  if (is_ios) {
    sources += [
      "passwords_directory_util_ios.cc",
      "passwords_directory_util_ios.h",
    ]
  }

  deps = [
    "//base",
    "//components/autofill/core/common",
    "//sql",
    "//url",
  ]

  public_deps = [
    ":constants",
    ":features",
  ]
}

source_set("constants") {
  sources = [ "password_manager_constants.h" ]
}

source_set("features") {
  sources = [
    "password_manager_features.cc",
    "password_manager_features.h",
  ]

  if (is_android) {
    sources += [ "password_manager_feature_variations_android.h" ]
  }

  deps = [
    "//base",
    "//build:blink_buildflags",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "password_manager_util_unittest.cc" ]

  deps = [
    ":common",
    "//components/autofill/core/common",
    "//testing/gtest",
  ]

  if (is_ios) {
    sources += [ "passwords_directory_util_ios_unittest.cc" ]

    deps += [
      "//base",
      "//base/test:test_support",
      "//sql",
      "//url",
    ]
  }
}

if (is_android) {
  java_cpp_enum("java_enums_srcjar") {
    visibility = [ ":*" ]
    sources = [ "password_manager_feature_variations_android.h" ]
  }

  android_library("password_manager_common_java_enums") {
    deps = [ "//third_party/androidx:androidx_annotation_annotation_java" ]
    srcjar_deps = [ ":java_enums_srcjar" ]
  }
}
