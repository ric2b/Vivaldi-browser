# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

if (is_android) {
  import("//build/config/android/rules.gni")
}

static_library("public") {
  sources = [
    "autofill_assistant.cc",
    "autofill_assistant.h",
    "autofill_assistant_factory.h",
    "external_action_delegate.h",
    "external_action_util.cc",
    "external_action_util.h",
    "headless_onboarding_result.h",
    "headless_script_controller.h",
    "prefs.cc",
    "prefs.h",
    "public_script_parameters.h",
    "rectf.cc",
    "rectf.h",
    "runtime_manager.cc",
    "runtime_manager.h",
    "runtime_manager_impl.cc",
    "runtime_manager_impl.h",
    "runtime_observer.h",
    "ui_state.h",
  ]

  deps = [
    ":proto",
    "//components/autofill/core/browser:browser",
    "//components/autofill/core/common:common",
    "//components/autofill_assistant/browser/public/fast_checkout/proto:proto",
    "//components/autofill_assistant/browser/public/password_change/proto:proto",
    "//components/prefs:prefs",
    "//url",
  ]

  public_deps = [
    "//base",
    "//components/version_info:channel",
    "//content/public/browser",
  ]
}

static_library("password_change") {
  sources = [
    "password_change/empty_website_login_manager_impl.cc",
    "password_change/empty_website_login_manager_impl.h",
    "password_change/features.cc",
    "password_change/features.h",
    "password_change/save_password_leak_detection_delegate.cc",
    "password_change/save_password_leak_detection_delegate.h",
    "password_change/website_login_manager.cc",
    "password_change/website_login_manager.h",
    "password_change/website_login_manager_impl.cc",
    "password_change/website_login_manager_impl.h",
  ]
  deps = [
    "//base",
    "//components/autofill/core/common",
    "//components/password_manager/content/browser",
    "//components/password_manager/core/browser",
    "//components/password_manager/core/browser/form_parsing",
    "//components/password_manager/core/browser/leak_detection:leak_detection",
    "//components/version_info:channel",
    "//content/public/browser",
  ]
}

proto_library("proto") {
  proto_in_dir = "//"
  sources = [ "external_action.proto" ]
  link_deps = [
    "//components/autofill_assistant/browser/public/assistant_ui/proto:proto",
    "//components/autofill_assistant/browser/public/fast_checkout/proto:proto",
    "//components/autofill_assistant/browser/public/password_change/proto:proto",
  ]
}

# Java protos are only used for testing.
if (is_android) {
  proto_java_library("proto_java") {
    proto_path = "//"
    sources = [ "external_action.proto" ]
    deps = [
      "//components/autofill_assistant/browser/public/assistant_ui/proto:proto_java",
      "//components/autofill_assistant/browser/public/fast_checkout/proto:proto_java",
      "//components/autofill_assistant/browser/public/password_change/proto:proto_java",
    ]
  }
}

static_library("unit_test_support") {
  testonly = true
  sources = [
    "mock_autofill_assistant.cc",
    "mock_autofill_assistant.h",
    "mock_external_action_delegate.cc",
    "mock_external_action_delegate.h",
    "mock_headless_script_controller.cc",
    "mock_headless_script_controller.h",
    "mock_runtime_manager.cc",
    "mock_runtime_manager.h",
    "password_change/mock_website_login_manager.cc",
    "password_change/mock_website_login_manager.h",
  ]

  deps = [
    ":password_change",
    ":public",
    "//base",
    "//components/password_manager/core/browser",
    "//content/public/browser",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "autofill_assistant_unittest.cc",
    "password_change/save_password_leak_detection_delegate_unittest.cc",
    "password_change/website_login_manager_impl_unittest.cc",
    "prefs_unittest.cc",
  ]
  deps = [
    ":password_change",
    ":public",
    ":unit_test_support",
    "//base",
    "//base/test:test_support",
    "//components/autofill/core/browser:test_support",
    "//components/password_manager/core/browser:test_support",
    "//components/password_manager/core/browser/leak_detection:test_support",
    "//components/password_manager/core/common",
    "//components/prefs:prefs",
    "//components/prefs:test_support",
    "//components/safe_browsing/core/common:common",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/version_info",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//third_party/re2",
  ]
}
