# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

assert(is_chromeos_ash)

preprocess_folder = "preprocessed"

# When adding a new file to personalization app, add it to one of the lists
# below. `non_web_component_files` are plain ts files, `web_component_files` are
# polymer based ts files, `css_wrapper_files` are Polymer css files,
# `static_resource_files` are non-js files, e.g. image, html, css
non_web_component_files = [
  "js/ambient/ambient_actions.ts",
  "js/ambient/ambient_controller.ts",
  "js/ambient/ambient_interface_provider.ts",
  "js/ambient/ambient_observer.ts",
  "js/ambient/ambient_reducers.ts",
  "js/ambient/ambient_state.ts",
  "js/ambient/utils.ts",
  "js/keyboard_backlight/keyboard_backlight_actions.ts",
  "js/keyboard_backlight/keyboard_backlight_controller.ts",
  "js/keyboard_backlight/keyboard_backlight_interface_provider.ts",
  "js/keyboard_backlight/keyboard_backlight_observer.ts",
  "js/keyboard_backlight/keyboard_backlight_reducers.ts",
  "js/keyboard_backlight/keyboard_backlight_state.ts",
  "js/personalization_actions.ts",
  "js/personalization_app.ts",
  "js/personalization_metrics_logger.ts",
  "js/personalization_reducers.ts",
  "js/personalization_state.ts",
  "js/personalization_store.ts",
  "js/personalization_test_api.ts",
  "js/theme/theme_actions.ts",
  "js/theme/theme_controller.ts",
  "js/theme/theme_interface_provider.ts",
  "js/theme/theme_observer.ts",
  "js/theme/theme_reducers.ts",
  "js/theme/theme_state.ts",
  "js/user/user_actions.ts",
  "js/user/user_controller.ts",
  "js/user/user_image_observer.ts",
  "js/user/user_interface_provider.ts",
  "js/user/user_reducers.ts",
  "js/user/user_selectors.ts",
  "js/user/user_state.ts",
  "js/user/webcam_utils_proxy.ts",
  "js/utils.ts",
  "js/wallpaper/constants.ts",
  "js/wallpaper/google_photos_metrics_logger.ts",
  "js/wallpaper/utils.ts",
  "js/wallpaper/wallpaper_actions.ts",
  "js/wallpaper/wallpaper_controller.ts",
  "js/wallpaper/wallpaper_interface_provider.ts",
  "js/wallpaper/wallpaper_observer.ts",
  "js/wallpaper/wallpaper_reducers.ts",
  "js/wallpaper/wallpaper_state.ts",
  "js/wallpaper/index.ts",
]

# Files holding a Polymer element definition AND have an equivalent .html file.
web_component_files = [
  "js/ambient/album_list_element.ts",
  "js/ambient/albums_subpage_element.ts",
  "js/ambient/art_album_dialog_element.ts",
  "js/ambient/ambient_preview_element.ts",
  "js/ambient/ambient_subpage_element.ts",
  "js/ambient/ambient_zero_state_svg_element.ts",
  "js/ambient/ambient_weather_element.ts",
  "js/ambient/animation_theme_item_element.ts",
  "js/ambient/animation_theme_list_element.ts",
  "js/ambient/toggle_row_element.ts",
  "js/ambient/topic_source_item_element.ts",
  "js/ambient/topic_source_list_element.ts",
  "js/ambient/zero_state_element.ts",
  "js/keyboard_backlight/keyboard_backlight_element.ts",
  "js/personalization_main_element.ts",
  "js/personalization_router_element.ts",
  "js/personalization_toast_element.ts",
  "js/personalization_breadcrumb_element.ts",
  "js/theme/personalization_theme_element.ts",
  "js/theme/color_scheme_icon_svg_element.ts",
  "js/theme/dynamic_color_element.ts",
  "js/user/avatar_camera_element.ts",
  "js/user/avatar_list_element.ts",
  "js/user/user_preview_element.ts",
  "js/user/user_subpage_element.ts",
  "js/wallpaper/google_photos_albums_element.ts",
  "js/wallpaper/google_photos_collection_element.ts",
  "js/wallpaper/google_photos_photos_by_album_id_element.ts",
  "js/wallpaper/google_photos_photos_element.ts",
  "js/wallpaper/google_photos_zero_state_element.ts",
  "js/wallpaper/local_images_element.ts",
  "js/wallpaper/wallpaper_collections_element.ts",
  "js/wallpaper/wallpaper_error_element.ts",
  "js/wallpaper/wallpaper_fullscreen_element.ts",
  "js/wallpaper/wallpaper_grid_item_element.ts",
  "js/wallpaper/wallpaper_images_element.ts",
  "js/wallpaper/wallpaper_preview_element.ts",
  "js/wallpaper/wallpaper_selected_element.ts",
  "js/wallpaper/wallpaper_subpage_element.ts",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

icons_html_files = [ "common/icons.html" ]

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files + icons_html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

ts_files = web_component_files + non_web_component_files

# Files that are passed as input to css_to_wrapper().
css_files = [
  "css/common.css",
  "css/cros_button_style.css",
  "css/wallpaper.css",
]

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".ts" ]
}

static_resource_files = [
  "hub_icon_64.png",
  "hub_icon_128.png",
  "hub_icon_192.png",
  "hub_icon_256.png",
  "icon_192.png",

  "index.html",

  "images/ambient_mode_disabled.svg",
  "images/ambient_mode_disabled_dark.svg",
  "images/feel_the_breeze.png",
  "images/float_on_by.png",
  "images/google_photos.svg",
  "images/no_images.svg",
  "images/no_google_photos_images.svg",
  "images/no_google_photos_images_dark.svg",
  "images/slideshow.png",

  "css/base.css",
]

css_to_wrapper("css_wrapper_files") {
  in_files = css_files
}

html_to_wrapper("html_to_wrapper_files") {
  in_files = html_files + icons_html_files
}

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = ts_files
}

preprocess_if_expr("preprocess_generated") {
  deps = [
    ":css_wrapper_files",
    ":html_to_wrapper_files",
  ]
  in_folder = target_gen_dir
  out_folder = "$target_gen_dir/$preprocess_folder"
  in_files = html_wrapper_files + css_wrapper_files
}

copy("copy_mojo_to_js") {
  deps = [ "../mojom:mojom_webui_js" ]
  sources = [ "$root_gen_dir/mojom-webui/ash/webui/personalization_app/mojom/personalization_app.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/$preprocess_folder/js/{{source_file_part}}" ]
}

ts_library("build_ts") {
  composite = true
  root_dir = "$target_gen_dir/$preprocess_folder"
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"

  in_files = ts_files + css_wrapper_files + html_wrapper_files +
             [ "js/personalization_app.mojom-webui.js" ]

  definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]

  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/mojo:library",
  ]

  extra_deps = [
    ":copy_mojo_to_js",
    ":preprocess",
    ":preprocess_generated",
    "//ash/webui/common/resources:generate_definitions",
  ]
}

if (optimize_webui) {
  build_manifest = "build_manifest.json"

  optimize_webui("build") {
    host = "personalization"
    input = rebase_path("$target_gen_dir/tsc", root_build_dir)
    js_out_files = [ "personalization_app.rollup.js" ]
    js_module_in_files = [ "js/personalization_app.js" ]
    out_manifest = "$target_gen_dir/$build_manifest"

    deps = [
      ":build_ts",
      "//ui/webui/resources:preprocess",
    ]

    excludes = [ "chrome://resources/mojo/mojo/public/js/bindings.js" ]
  }
}

generate_grd("build_grd") {
  input_files_base_dir = rebase_path(".", "//")
  input_files = static_resource_files

  if (optimize_webui) {
    deps = [ ":build" ]
    manifest_files = [ "$target_gen_dir/$build_manifest" ]
    resource_path_rewrites =
        [ "personalization_app.rollup.js|js/personalization_app.js" ]
  } else {
    deps = [ ":build_ts" ]
    manifest_files =
        filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  }
  grd_prefix = "ash_personalization_app"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
}
