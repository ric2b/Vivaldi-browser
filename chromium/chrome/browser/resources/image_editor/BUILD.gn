# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/browser/resources/tools/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

grd_prefix = "image_editor"
preprocess_folder = "$target_gen_dir/preprocessed"
preprocess_manifest = "preprocessed_manifest.json"

ts_library("build_ts") {
  root_dir = preprocess_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "untrusted.ts",
    "image_editor.mojom-webui.js",
  ]
  deps = [ "//ui/webui/resources/mojo:library" ]
  extra_deps = [
    ":copy_mojo",
    ":preprocess_untrusted",
  ]
}

preprocess_if_expr("preprocess_untrusted") {
  in_folder = "./untrusted/"  #target_gen_dir
  out_folder = preprocess_folder
  out_manifest = "$target_gen_dir/untrusted/$preprocess_manifest"
  in_files = [
    "untrusted.ts",
    "untrusted.html",
  ]
}

copy("copy_mojo") {
  deps =
      [ "//chrome/browser/ui/webui/image_editor:mojo_bindings_js__generator" ]
  sources = [ "$root_gen_dir/mojom-webui/chrome/browser/ui/webui/image_editor/image_editor.mojom-webui.js" ]
  outputs = [ "$preprocess_folder/{{source_file_part}}" ]
}

generate_grd("build_trusted_grd") {
  grd_prefix = grd_prefix
  out_grd = "$target_gen_dir/resources.grd"
  input_files = [ "image_editor.html" ]

  input_files_base_dir = rebase_path(".", "//")
}

generate_grd("build_untrusted_grd") {
  grd_prefix = "image_editor_untrusted"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [ "untrusted.html" ]
  input_files_base_dir = rebase_path(preprocess_folder, root_build_dir)
  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
}

grit("trusted_resources") {
  source = "$target_gen_dir/resources.grd"
  enable_input_discovery_for_gn_analyze = false
  deps = [ ":build_trusted_grd" ]

  outputs = [
    "grit/image_editor_resources.h",
    "grit/image_editor_resources_map.cc",
    "grit/image_editor_resources_map.h",
    "image_editor_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}

grit("untrusted_resources") {
  source = "$target_gen_dir/image_editor_untrusted_resources.grd"
  enable_input_discovery_for_gn_analyze = false
  deps = [ ":build_untrusted_grd" ]

  outputs = [
    "grit/image_editor_untrusted_resources.h",
    "grit/image_editor_untrusted_resources_map.cc",
    "grit/image_editor_untrusted_resources_map.h",
    "image_editor_untrusted_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
