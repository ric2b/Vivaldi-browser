# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("./modules/modules.gni")
import("./realbox/realbox.gni")

# List of files that don't need to be passed to html_to_wrapper().
non_web_component_files =
    [
      "background_manager.ts",
      "customize_dialog_types.ts",
      "i18n_setup.ts",
      "lazy_load.ts",
      "metrics_utils.ts",
      "new_tab_page_proxy.ts",
      "new_tab_page.ts",
      "utils.ts",
      "window_proxy.ts",
    ] + modules_non_web_component_files + realbox_non_web_component_files

# Files holding a Polymer element definition and have an equivalent .html file.
web_component_files =
    [
      "app.ts",
      "customize_backgrounds.ts",
      "customize_dialog.ts",
      "customize_modules.ts",
      "customize_shortcuts.ts",
      "doodle_share_dialog.ts",
      "iframe.ts",
      "lens_form.ts",
      "lens_upload_dialog.ts",
      "logo.ts",
      "middle_slot_promo.ts",
      "mini_page.ts",
      "voice_search_overlay.ts",
    ] + modules_web_component_files + realbox_web_component_files

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".ts", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".ts" ]
}

# Files that are passed as input to css_to_wrapper().
css_files = realbox_css_files

# Files that are generated by css_to_wrapper().
css_wrapper_files = []
foreach(f, css_files) {
  css_wrapper_files += [ f + ".ts" ]
}

ts_files = web_component_files + non_web_component_files

mojo_ts_files = [
  "chrome_cart.mojom-webui.ts",
  "drive.mojom-webui.ts",
  "feed.mojom-webui.ts",
  "new_tab_page.mojom-webui.ts",
  "photos.mojom-webui.ts",
  "recipes.mojom-webui.ts",
  "omnibox.mojom-webui.ts",
]

if (!is_official_build) {
  mojo_ts_files += [ "foo.mojom-webui.ts" ]
}

mojo_js_files = []
foreach(f, mojo_ts_files) {
  mojo_js_files += [ string_replace(f, ".ts", ".js") ]
}
