# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//ui/webui/resources/tools/build_webui.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

if (is_chrome_branded) {
  preprocess_folder = "$target_gen_dir/preprocessed"
}
build_webui("build") {
  grd_prefix = "data_sharing"

  ts_deps = [
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:build_ts",
  ]

  mojo_files_deps =
      [ "//chrome/browser/ui/webui/data_sharing:mojo_bindings_ts__generator" ]
  mojo_files = [ "$root_gen_dir/chrome/browser/ui/webui/data_sharing/data_sharing.mojom-webui.ts" ]

  static_files = [
    "data_sharing.html",
    "data_sharing_api.html",
  ]

  non_web_component_files = [
    "browser_proxy.ts",
    "browser_proxy_api.ts",
    "browser_proxy_base.ts",
    "data_sharing.ts",
    "data_sharing_api.ts",
  ]

  if (is_chrome_branded) {
    ts_extra_deps = [ ":copy_data_sharing_sdk" ]
    extra_grdp_deps = [ ":build_data_sharing_sdk_grdp" ]
    extra_grdp_files = [ "$target_gen_dir/data_sharing_sdk.grdp" ]

    ts_definitions = [ "$preprocess_folder/data_sharing_sdk.d.ts" ]
  } else {
    static_files += [ "data_sharing_sdk.css" ]

    non_web_component_files += [ "data_sharing_sdk.ts" ]
  }

  webui_context_type = "untrusted"
}

if (is_chrome_branded) {
  generate_grd("build_data_sharing_sdk_grdp") {
    grd_prefix = "data_sharing_sdk"
    out_grd = "$target_gen_dir/data_sharing_sdk.grdp"
    input_files = [
      "data_sharing_sdk.js",
      "data_sharing_sdk.css",
    ]
    input_files_base_dir = rebase_path(preprocess_folder, root_build_dir)
  }

  copy("copy_data_sharing_sdk") {
    sources = [
      "//third_party/data_sharing_sdk/data_sharing_sdk.css",
      "//third_party/data_sharing_sdk/data_sharing_sdk.d.ts",
      "//third_party/data_sharing_sdk/data_sharing_sdk.js",
    ]

    outputs = [ "$preprocess_folder/{{source_file_part}}" ]
  }
}
