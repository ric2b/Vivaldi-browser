# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")

assert(is_chromeos)

source_set("quick_answers") {
  sources = [
    "quick_answers_controller_impl.cc",
    "quick_answers_controller_impl.h",
    "quick_answers_ui_controller.cc",
    "quick_answers_ui_controller.h",
    "ui/quick_answers_text_label.cc",
    "ui/quick_answers_text_label.h",
    "ui/quick_answers_util.cc",
    "ui/quick_answers_util.h",
    "ui/quick_answers_view.cc",
    "ui/quick_answers_view.h",
    "ui/rich_answers_definition_view.cc",
    "ui/rich_answers_definition_view.h",
    "ui/rich_answers_translation_view.cc",
    "ui/rich_answers_translation_view.h",
    "ui/rich_answers_unit_conversion_view.cc",
    "ui/rich_answers_unit_conversion_view.h",
    "ui/rich_answers_view.cc",
    "ui/rich_answers_view.h",
    "ui/user_consent_view.cc",
    "ui/user_consent_view.h",
  ]

  configs += [ "//build/config/compiler:wexit_time_destructors" ]

  deps = [
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/chromeos/read_write_cards",
    "//chrome/browser/ui/color:color_headers",
    "//chrome/browser/ui/quick_answers/ui:loading_view",
    "//chrome/browser/ui/quick_answers/ui:quick_answers_stage_button",
    "//chrome/browser/ui/quick_answers/ui:result_view",
    "//chrome/browser/ui/quick_answers/ui:retry_view",
    "//chrome/browser/ui/views/editor_menu:utils",
    "//chromeos/components/quick_answers",
    "//chromeos/components/quick_answers/public/cpp:cpp",
    "//chromeos/components/quick_answers/public/cpp:prefs",
    "//chromeos/constants:constants",
    "//chromeos/strings:strings_grit",
    "//components/account_id",
    "//components/omnibox/browser:vector_icons",
    "//components/prefs",
    "//content/public/browser",
    "//ui/views/controls/webview",
    "//ui/wm",
  ]

  if (is_chromeos_ash) {
    sources += [
      "quick_answers_state_ash.cc",
      "quick_answers_state_ash.h",
    ]
    deps += [
      "//ash",
      "//chromeos/components/kiosk:kiosk",
      "//components/language/core/browser:browser",
    ]
  }

  if (is_chromeos_lacros) {
    sources += [
      "lacros/quick_answers_state_lacros.cc",
      "lacros/quick_answers_state_lacros.h",
    ]
    deps += [
      "//chrome/browser/ui",
      "//chromeos/components/kiosk:kiosk",
      "//chromeos/lacros:lacros",
    ]
  }
}
