# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This is the public interface for tabs in a desktop browser. Most features in
# //chrome/browser depend on this interface, and thus to prevent circular
# dependencies this interface should not depend on anything else in //chrome.
source_set("tabs") {
  public = [
    "public/tab_features.h",
    "public/tab_interface.h",
    "tab_network_state.h",
  ]
  sources = [ "tab_network_state.cc" ]
  public_deps = [ "//base" ]
  deps = [ "//content/public/browser" ]
}

# These enums should not need to depend on chrome/browser/ui. They have been
# broken out of the chrome/browser/ui build file into a separate target to
# prevent a circular dependency between mojo targets that chrome/browser/ui
# depends on which have these enums in their dependency chain.
source_set("tab_enums") {
  sources = [ "tab_enums.h" ]
}

# This is pulled out in a standalone target as its include is spread
# all around //chrome/browser and it was part of //chrome/browser/ui:ui.
# As a result, it created quite a few of circular dependencies when
# directories in //c/b and //c/b/ui began to be modularized.
# TODO(crbug.com/364501603): modularize all the rest.
if (!is_android) {
  source_set("tab_strip_model_observer") {
    sources = [
      "tab_change_type.h",
      "tab_strip_model_observer.h",
    ]
    public_deps = [
      "//base",
      "//components/sessions:session_id",
      "//components/tab_groups",
      "//ui/base",
    ]
  }

  source_set("tab_strip_model_observer_impl") {
    sources = [ "tab_strip_model_observer.cc" ]
    public_deps = [ "//chrome/browser:browser_public_dependencies" ]
    deps = [
      ":tab_strip_model_observer",
      "//base",
      "//content/public/browser",
    ]
  }
}
