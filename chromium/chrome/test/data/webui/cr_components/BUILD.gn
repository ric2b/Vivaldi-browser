# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//crypto/features.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android && !is_ios)

preprocessed_files = [ "managed_footnote_test.ts" ]

if (use_nss_certs) {
  # Test files that contain // <if expr> and therefore require preprocessing.
  preprocessed_files += [ "certificate_manager_test.ts" ]
  if (is_chromeos) {
    preprocessed_files += [ "certificate_manager_provisioning_test.ts" ]
  }
}

# Test files that do not require preprocessing. If adding // <if expr> to any
# file below, move it to the list above.
non_preprocessed_files = [
  "app_management/app_management_test_support.ts",
  "app_management/file_handling_item_test.ts",
  "app_management/permission_item_test.ts",
  "app_management/window_mode_item_test.ts",
  "app_management/uninstall_button_test.ts",
  "color_change_listener_test.ts",
  "customize_themes_test.ts",
  "help_bubble_mixin_test.ts",
  "help_bubble_test.ts",
  "history_clusters_test.ts",
  "localized_link_test.ts",
  "managed_dialog_test.ts",
  "most_visited_focus_test.ts",
  "most_visited_test.ts",
  "most_visited_test_support.ts",
]

preprocessed_folder = "$target_gen_dir/preprocessed"

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = preprocessed_folder
  in_files = preprocessed_files
}

copy("copy") {
  sources = non_preprocessed_files
  outputs = [ "$preprocessed_folder/{{source_target_relative}}" ]
}

ts_library("build_ts") {
  root_dir = preprocessed_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [ "chrome://webui-test/*|" +
                    rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                                target_gen_dir) ]
  in_files = non_preprocessed_files + preprocessed_files
  deps = [
    "..:build_ts",
    "//ui/webui/resources/cr_components/app_management:build_ts",
    "//ui/webui/resources/cr_components/color_change_listener:build_ts",
    "//ui/webui/resources/cr_components/customize_themes:build_ts",
    "//ui/webui/resources/cr_components/help_bubble:build_ts",
    "//ui/webui/resources/cr_components/history_clusters:build_ts",
    "//ui/webui/resources/cr_components/most_visited:build_ts",
  ]

  if (use_nss_certs) {
    deps +=
        [ "//ui/webui/resources/cr_components/certificate_manager:build_ts" ]
  }

  extra_deps = [
    ":copy",
    ":preprocess",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_cr_components"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "cr_components"
}
