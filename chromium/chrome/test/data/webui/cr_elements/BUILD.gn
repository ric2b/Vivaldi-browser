# Copyright 2019 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android && !is_ios)

# Test files that contain // <if expr> and therefore require preprocessing.
preprocessed_files = [
  "cr_policy_indicator_mixin_tests.ts",
  "cr_policy_indicator_tests.ts",
  "cr_policy_pref_indicator_tests.ts",
  "cr_policy_strings.ts",
]

# Test files that do not require preprocessing. If adding // <if expr> to any
# file below, move it to the list above.
non_preprocessed_files = [
  "cr_a11y_announcer_test.ts",
  "cr_action_menu_test.ts",
  "cr_auto_img_test.ts",
  "cr_button_tests.ts",
  "cr_card_radio_button_test.ts",
  "cr_checkbox_test.ts",
  "cr_container_shadow_mixin_test.ts",
  "cr_dialog_test.ts",
  "cr_drawer_tests.ts",
  "cr_expand_button_tests.ts",
  "cr_fingerprint_progress_arc_tests.ts",
  "cr_grid_focus_test.ts",
  "cr_icon_button_tests.ts",
  "cr_input_test.ts",
  "cr_lazy_render_tests.ts",
  "cr_link_row_tests.ts",
  "cr_lottie_tests.ts",
  "cr_menu_selector_focus_test.ts",
  "cr_profile_avatar_selector_tests.ts",
  "cr_radio_button_test.ts",
  "cr_radio_group_test.ts",
  "cr_search_field_tests.ts",
  "cr_scrollable_mixin_tests.ts",
  "cr_slider_test.ts",
  "cr_splitter_test.ts",
  "cr_tabs_test.ts",
  "cr_toast_manager_test.ts",
  "cr_toast_test.ts",
  "cr_toggle_test.ts",
  "cr_toolbar_focus_tests.ts",
  "cr_toolbar_search_field_tests.ts",
  "cr_toolbar_test.ts",
  "cr_tree_test.ts",
  "cr_view_manager_test.ts",
  "find_shortcut_mixin_test.ts",
  "i18n_mixin_test.ts",
  "iron_list_focus_test.ts",
  "list_property_update_mixin_tests.ts",
  "web_ui_listener_mixin_test.ts",
]

if (is_chromeos_ash) {
  non_preprocessed_files += [ "cr_searchable_drop_down_tests.ts" ]
}

preprocessed_folder = "$target_gen_dir/preprocessed"

preprocess_if_expr("preprocess") {
  in_folder = "."
  out_folder = preprocessed_folder
  in_files = preprocessed_files
}

copy("copy") {
  sources = non_preprocessed_files
  outputs = [ "$preprocessed_folder/{{source_target_relative}}" ]
}

ts_library("build_ts") {
  root_dir = preprocessed_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [ "chrome://webui-test/*|" +
                    rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                                target_gen_dir) ]
  in_files = non_preprocessed_files + preprocessed_files
  definitions = [
    "//tools/typescript/definitions/pending.d.ts",
    "//tools/typescript/definitions/settings_private.d.ts",
  ]
  deps = [
    "..:build_ts",
    "//ui/webui/resources:library",
  ]
  extra_deps = [
    ":copy",
    ":preprocess",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_cr_elements"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "cr_elements"
}
