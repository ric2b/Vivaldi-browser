# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//components/signin/features.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(enable_dice_support)

# Test files that do not require preprocessing. If adding // <if expr> to any
# file below, create a processed_files list and add it to it
non_preprocessed_files = [
  "dice_app_test.ts",
  "sign_in_promo_test.ts",
  "test_intro_browser_proxy.ts",
]

preprocessed_folder = "$target_gen_dir/preprocessed"

copy("copy") {
  sources = non_preprocessed_files
  outputs = [ "$preprocessed_folder/{{source_target_relative}}" ]
}

ts_library("build_ts") {
  root_dir = preprocessed_folder
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://intro/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/intro/tsc/*",
                    target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = non_preprocessed_files
  deps = [
    "..:build_ts",
    "//chrome/browser/resources/intro:build_ts",
  ]
  extra_deps = [ ":copy" ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_intro"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "intro"
}
