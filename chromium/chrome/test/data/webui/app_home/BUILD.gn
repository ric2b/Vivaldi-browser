# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(is_win || is_mac || is_linux || is_fuchsia)

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://apps/*|" +
        rebase_path("$root_gen_dir/chrome/browser/resources/app_home/tsc/*",
                    target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = [
    "app_list_test.ts",
    "test_app_home_browser_proxy.ts",
  ]
  definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  deps = [
    "..:build_ts",
    "//chrome/browser/resources/app_home:build_ts",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_app_home"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "app_home"
}
