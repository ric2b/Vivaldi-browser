# Copyright 2020 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//ui/webui/webui_features.gni")
import("../../tools/build_cr_component.gni")

assert(!is_android && !is_ios)

mojom("mojom") {
  sources = [ "customize_themes.mojom" ]

  public_deps = [
    "//mojo/public/mojom/base",
    "//skia/public/mojom",
  ]

  webui_module_path = "chrome://resources/cr_components/customize_themes/"
  use_typescript_sources = true
}

build_cr_component("build") {
  grd_prefix = "cr_components_customize_themes"

  static_files = [
    "colorize.svg",
    "brush.svg",
  ]

  web_component_files = [
    "customize_themes.ts",
    "theme_icon.ts",
  ]

  non_web_component_files = [ "browser_proxy.ts" ]

  mojo_files_deps = [ ":mojom_ts__generator" ]
  mojo_files = [ "$target_gen_dir/customize_themes.mojom-webui.ts" ]

  tsc_dir =
      "$root_gen_dir/ui/webui/resources/tsc/cr_components/customize_themes"
  ts_deps = [
    "../managed_dialog:build_ts",
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources/cr_elements:build_ts",
    "//ui/webui/resources/js:build_ts",
    "//ui/webui/resources/mojo:library",
  ]

  optimize = optimize_webui
}
