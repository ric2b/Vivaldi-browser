# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(!is_android && !is_ios)

preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/cr_components/color_change_listener"

mojom("mojom") {
  sources = [ "color_change_listener.mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
  webui_module_path = "chrome://resources/cr_components/color_change_listener/"
}

copy("copy_mojom") {
  deps = [ ":mojom_js__generator" ]
  sources = [ "$root_gen_dir/mojom-webui/ui/webui/resources/cr_components/color_change_listener/color_change_listener.mojom-webui.js" ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

copy("copy_src") {
  sources = [
    "browser_proxy.ts",
    "colors_css_updater.ts",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("build_ts") {
  root_dir = target_gen_dir
  out_dir = preprocess_folder
  composite = true
  tsconfig_base = "tsconfig_base.json"
  in_files = [
    "browser_proxy.ts",
    "colors_css_updater.ts",
    "color_change_listener.mojom-webui.js",
  ]
  deps = [ "//ui/webui/resources/mojo:library" ]
  extra_deps = [
    ":copy_mojom",
    ":copy_src",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "cr_components_color_change_listener"
  out_grd = "$target_gen_dir/resources.grdp"
  public_deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "cr_components/color_change_listener"
}
