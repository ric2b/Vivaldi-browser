# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/typescript/ts_library.gni")

tsc_folder = "$root_gen_dir/ui/webui/resources/tsc/mojo"

mojo_js_files = [
  "mojo/public/mojom/base/big_buffer.mojom-webui.js",
  "mojo/public/mojom/base/file.mojom-webui.js",
  "mojo/public/mojom/base/file_path.mojom-webui.js",
  "mojo/public/mojom/base/int128.mojom-webui.js",
  "mojo/public/mojom/base/process_id.mojom-webui.js",
  "mojo/public/mojom/base/read_only_buffer.mojom-webui.js",
  "mojo/public/mojom/base/safe_base_name.mojom-webui.js",
  "mojo/public/mojom/base/string16.mojom-webui.js",
  "mojo/public/mojom/base/text_direction.mojom-webui.js",
  "mojo/public/mojom/base/time.mojom-webui.js",
  "mojo/public/mojom/base/token.mojom-webui.js",
  "mojo/public/mojom/base/unguessable_token.mojom-webui.js",
  "skia/public/mojom/skcolor.mojom-webui.js",
  "skia/public/mojom/bitmap.mojom-webui.js",
  "skia/public/mojom/image_info.mojom-webui.js",
  "ui/base/mojom/window_open_disposition.mojom-webui.js",
  "ui/gfx/geometry/mojom/geometry.mojom-webui.js",
  "ui/gfx/image/mojom/image.mojom-webui.js",
  "url/mojom/url.mojom-webui.js",
  "url/mojom/origin.mojom-webui.js",
]

if (is_chromeos_ash) {
  mojo_js_files += [
    "chromeos/ash/services/auth_factor_config/public/mojom/auth_factor_config.mojom-webui.js",
    "chromeos/ash/services/bluetooth_config/public/mojom/cros_bluetooth_config.mojom-webui.js",
    "chromeos/ash/services/cellular_setup/public/mojom/cellular_setup.mojom-webui.js",
    "chromeos/ash/services/cellular_setup/public/mojom/esim_manager.mojom-webui.js",
    "chromeos/ash/services/hotspot_config/public/mojom/cros_hotspot_config.mojom-webui.js",
    "chromeos/ash/services/nearby/public/mojom/nearby_share_settings.mojom-webui.js",
    "chromeos/ash/services/nearby/public/mojom/nearby_share_target_types.mojom-webui.js",
    "chromeos/services/network_config/public/mojom/cros_network_config.mojom-webui.js",
    "chromeos/services/network_config/public/mojom/network_types.mojom-webui.js",
    "chromeos/services/network_health/public/mojom/network_diagnostics.mojom-webui.js",
    "chromeos/services/network_health/public/mojom/network_health.mojom-webui.js",
    "chromeos/services/network_health/public/mojom/network_health_types.mojom-webui.js",
    "services/network/public/mojom/ip_address.mojom-webui.js",
    "ui/base/accelerators/mojom/accelerator.mojom-webui.js",
    "ui/gfx/range/mojom/range.mojom-webui.js",
  ]
}

# Move all the files out of mojom-webui to the expected location.
preprocess_if_expr("copy_mojo") {
  visibility = [ ":library" ]

  in_folder = "$root_gen_dir/mojom-webui"
  out_folder = "$target_gen_dir/preprocessed"
  in_files = mojo_js_files

  deps = [
    "//mojo/public/mojom/base:base_js__generator",
    "//skia/public/mojom:mojom_js__generator",
    "//ui/base/mojom:mojom_js__generator",
    "//ui/gfx/geometry/mojom:mojom_js__generator",
    "//ui/gfx/image/mojom:mojom_js__generator",
    "//url/mojom:url_mojom_gurl_js__generator",
    "//url/mojom:url_mojom_origin_js__generator",
  ]

  if (is_chromeos_ash) {
    deps += [
      "//chromeos/ash/services/auth_factor_config/public/mojom:mojom_js__generator",
      "//chromeos/ash/services/bluetooth_config/public/mojom:mojom_js__generator",
      "//chromeos/ash/services/cellular_setup/public/mojom:mojom_js__generator",
      "//chromeos/ash/services/hotspot_config/public/mojom:mojom_js__generator",
      "//chromeos/ash/services/nearby/public/mojom:nearby_share_settings_js__generator",
      "//chromeos/services/network_config/public/mojom:mojom_js__generator",
      "//chromeos/services/network_config/public/mojom:network_types_js__generator",
      "//chromeos/services/network_health/public/mojom:mojom_js__generator",
      "//chromeos/services/network_health/public/mojom:types_js__generator",
      "//services/network/public/mojom:mojom_ip_address_js__generator",
      "//ui/base/accelerators/mojom:mojom_js__generator",
      "//ui/gfx/range/mojom:mojom_js__generator",
    ]
  }
}

ts_library("library") {
  root_dir = "$target_gen_dir/preprocessed"
  out_dir = tsc_folder
  composite = true
  allow_js = true

  in_files = mojo_js_files

  definitions = [ "$tsc_folder/mojo/public/js/bindings.d.ts" ]

  extra_deps = [
    ":copy_bindings_dts",
    ":copy_mojo",
  ]

  path_mappings = [ "//resources/mojo/*|" +
                    rebase_path("$root_gen_dir/ui/webui/resources/tsc/mojo/*",
                                target_gen_dir) ]
}

# Copy bindings.d.ts file under the same location where other shared Mojo JS
# generated files reside, so that it can be located by the TS compiler when
# resolving chrome://resources/mojo/mojo/public/js/bindings.js.
copy("copy_bindings_dts") {
  sources = [ "bindings.d.ts" ]
  outputs = [ "$tsc_folder/mojo/public/js/{{source_file_part}}" ]
}
