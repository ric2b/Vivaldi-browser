# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/linux/gtk/gtk.gni")
import("//ui/qt/qt.gni")

assert(is_linux)

component("linux_ui") {
  defines = [ "IS_LINUX_UI_IMPL" ]
  public = [
    "cursor_theme_manager_observer.h",
    "device_scale_factor_observer.h",
    "linux_ui.h",
    "linux_ui_delegate.h",
    "linux_ui_delegate_stub.h",
    "nav_button_provider.h",
    "status_icon_linux.h",
    "window_button_order_observer.h",
    "window_frame_provider.h",
  ]
  sources = [
    "linux_ui.cc",
    "linux_ui_delegate.cc",
    "linux_ui_delegate_stub.cc",
    "status_icon_linux.cc",
  ]
  deps = [ "//base" ]
  public_deps = [ "//printing/buildflags" ]
}

source_set("linux_ui_impl") {
  public = [ "linux_ui_base.h" ]
  sources = [ "linux_ui_base.cc" ]
  public_deps = [ ":linux_ui" ]
  deps = [
    "//base",
    "//skia",
    "//ui/gfx",
    "//ui/native_theme",
    "//ui/shell_dialogs",
  ]
}

source_set("linux_ui_factory") {
  sources = [
    "linux_ui_factory.cc",
    "linux_ui_factory.h",
  ]

  public_deps = [ ":linux_ui" ]

  deps = [
    "//base",
    "//ui/base:buildflags",
  ]
  if (use_gtk) {
    # This is the only component that can interact with gtk.
    deps += [ "//ui/gtk" ]
  }
  if (use_qt) {
    deps += [ "//ui/qt" ]
  }
}

source_set("test_support") {
  testonly = true

  public = [ "fake_linux_ui.h" ]
  sources = [ "fake_linux_ui.cc" ]

  public_deps = [ ":linux_ui_impl" ]
  deps = [
    "//base",
    "//skia",
    "//ui/gfx",
    "//ui/native_theme",
    "//ui/shell_dialogs",
  ]
}
