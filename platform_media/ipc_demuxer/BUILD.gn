import("//build/config/features.gni")
import("//vivaldi/gn/config/features.gni")

import("//mojo/public/tools/bindings/mojom.gni")

if (vivaldi_use_system_media_demuxer) {

source_set("pipeline_types") {
  sources = [
    "platform_ipc_util.h",
    "platform_media_pipeline_types.cc",
    "platform_media_pipeline_types.h",
  ]

  public_deps = [
    "//ui/gfx/geometry:geometry",
    "//base",
    "//skia",
    "//media/gpu:buildflags",
  ]
}

mojom("mojo") {
  sources = [ "platform_media.mojom" ]
  input_root_override = "//vivaldi/"

  public_deps = [
    "//mojo/public/mojom/base",
    "//ui/gfx/geometry/mojom:mojom",
  ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "platform_media.mojom.StreamType"
          cpp = "::media::PlatformStreamType"
        },
        {
          mojom = "platform_media.mojom.TimeInfo"
          cpp = "::media::PlatformMediaTimeInfo"
        },
        {
          mojom = "platform_media.mojom.AudioConfig"
          cpp = "::media::PlatformAudioConfig"
        },
        {
          mojom = "platform_media.mojom.VideoPlaneConfig"
          cpp = "::media::PlatformVideoConfig::Plane"
        },
        {
          mojom = "platform_media.mojom.VideoConfig"
          cpp = "::media::PlatformVideoConfig"
        },
      ]
      traits_headers = [
        "//vivaldi/platform_media/ipc_demuxer/platform_media_mojom_traits.h"
      ]
      traits_sources = [
        "//vivaldi/platform_media/ipc_demuxer/platform_media_mojom_traits.cc"
      ]
      traits_public_deps = [
        ":pipeline_types",
      ]
    },
  ]
}

}
