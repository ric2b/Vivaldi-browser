import("//build/config/chrome_build.gni")

_main_dll_targets = []
if (is_win) {
  _main_dll_targets += [
    "//chrome:main_dll",
  ]

  if (is_multi_dll_chrome) {
    _main_dll_targets += [
      "//chrome:chrome_child",
    ]
  }
}
if (!is_mac && !is_win && !is_android) {
  _main_dll_targets += [ "//chrome:chrome_initial" ]
}
if (is_mac) {
  _main_dll_targets += [ "//chrome:chrome_dll" ]
}
foreach(_target, _main_dll_targets) {
  update_target(_target) {
    sources += [
      "//vivaldi/extraparts/vivaldi_main_delegate.cpp",
      "//vivaldi/extraparts/vivaldi_main_delegate.h",
    ]
    if (is_win) {
      sources += [
        "//vivaldi/browser/win/vivaldi_running_dlg.h",
        "//vivaldi/browser/win/vivaldi_running_dlg.cc",
      ]
    }
    deps += [
      "//vivaldi/components:adverse_adblocking",
    ]
    if (_target != "//chrome:chrome_child") {
      deps += [
        "//vivaldi/components:content_adverse_adblocking",
      ]
    }
    if (!is_android) {
      deps += [ "//vivaldi/extensions:extensions_client" ]
    }
  }
}

update_target("//chrome/test:test_support") {
  sources += [
    "//vivaldi/extraparts/vivaldi_main_delegate.cpp",
    "//vivaldi/extraparts/vivaldi_main_delegate.h",
  ]
  public_deps += [
    "//vivaldi/components:adverse_adblocking",
    "//vivaldi/components:content_adverse_adblocking",
  ]
  if (!is_android) {
    public_deps += [ "//vivaldi/extensions:extensions_client" ]
  }
}
